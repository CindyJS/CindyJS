<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8">
    <title>Cindy JS</title>
    <script type="text/javascript" src="../../build/js/Cindy.js"></script>
    <script type="text/javascript" src="../../build/js/CindyGL.js"></script>
    <link rel="stylesheet" href="../../css/cindy.css">
  </head>
    
	<body style="font-family:Arial;">
    
    <h1>CindyJS: Raytracer</h1>
    
    
    <script id="csdraw" type="cindyscript">
      time = seconds()-t0;
      alpha = .5+mouse().x;
      
      
    
     // ind = -10..10;
     // forall(ind, x,
     //   forall(ind, y,
     //    draw((x, y), size->10, color->computeColor((x, y)));
     //   )
     // );
      
      //X.color = computeColor(X);
      
      colorplot([-10,-10], [10,-10], "plot", computeColor(#));
      drawimage([-10,-10], [10,-10], "plot");
    </script>
               
    <script id="csinit" type="cindyscript">
      alpha = .8;
      F(p) := (
        regional(x, y, z, s);
        x = p.x; //TODO: implement .x 
        y = p.y;
        z = p.z;
        //x*x+y*y+z*z-1
        
        //alpha = 2.3+.35*sin(time);
         //x*x+y*y+z*z+alpha*x*y*z-1.
        s = x+y+z+1.;
        4.*(x*x*x+y*y*y+z*z*z+1.)-alpha*s*s*s;
      );
      
      dF(p) := (
        regional(x, y, z, s);
        x = p.x;
        y = p.y;
        z = p.z;
        //(2*x,2*y,2*z)
        s = x+y+z+1.;
        (
          12.*x*x-3.*alpha*s*s,
          12.*y*y-3.*alpha*s*s,
          12.*z*z-3.*alpha*s*s
        )
      );
      
      ray(pos, t) := (
        ([[cos(time/5+mouse().y/3),0.,sin(time/5+mouse().y/3)],
         [0.,1.,0.],
         [-sin(time/5+mouse().y/3),0.,cos(time/5+mouse().y/3)]]*
        [[1.,0,0.],
         [0,cos(mouse().x/3),sin(mouse().x/3)],
         [0,-sin(mouse().x/3),cos(mouse().x/3)]])*
          (t*(pos.x, pos.y, 10)+(0, 0, -2))
      );
      
      eval(poly, t) := (((poly_4)*t+poly_3)*t+poly_2)*t+poly_1; //evals deg 3 poly at time t, using horner scheme
      
      d(poly) := (poly_2, 2*poly_3, 3*poly_4, 0); //computes derivative of polynomial
      
      
      bisect(poly, l, u, def) := ( //bisection in [l, u]
        if(l < u,
          lv = eval(poly, l);
          uv = eval(poly, u);
          if(lv*uv< 0
            repeat(20,
              m = (l+u)/2;
              mv = eval(p, m);
              if(lv*mv > 0,
                l = m,
                u = m);
            ),
            def),
          def
        )
      );
      
      
      path(t) :=  t+i*4.12*t*(t-1); //a non-trivial path connecting 0 and 1 in C
      
      traceroot(p0, p1, r) := (
        //traces the root r of p0 to a root of p1 using homotopy p0->p1
        dp0 = d(p0);
        dp1 = d(p1);
        
        t = 0;
        repeat(21,
          a1 = path(t);
          a0 = 1-a1;
          r = r - (a0*eval(p0,r)+a1*eval(p1,r))/(a0*eval(dp0,r)+a1*eval(dp1,r)); // complex newton step on function homoptopy ((1-a1)*f0+a1*f1)
          t = t + 1/20;
        );
        r = r - eval(p1,r)/eval(dp1,r);
        r = r - eval(p1,r)/eval(dp1,r);
        r = r - eval(p1,r)/eval(dp1,r);
        r
      );
      
      makebetter(old, new) := if(abs(im(new))<0.01, //& l>= re(new) & re(new) <= u
          min(re(new), old),
        old);
      
      oo = 1000;
      
      firstroot(poly) := ( //finds first root of poly in interval [l, u]. returns oo if there is none
        p0 = [0,-1,0,1]; //x^3 - x has roots -1, 0, 1
        res = oo;
        res = makebetter(res, traceroot(p0, poly, -1));
        res = makebetter(res, traceroot(p0, poly, 0));
        res = makebetter(res, traceroot(p0, poly, 1));
        res
      );
      
      A = transpose([
        [1, 0, 0, 0],
        [0, 5, 10, 15],
        [0, 5*5, 10*10, 15*15],
        [0, 5*5*5, 10*10*10, 15*15*15]
      ]); // A sends polynomials [p0, p1, p2, p3] = p0+p1*X+p2*X*X+p3*X*X*X to [p(0), p(5), p(10), p(15)]
      B = inverse(A); //B interpolates polynomials, given the values [p(0), p(5), p(10), p(15)]
      
      computeColor(pos) := (
        polyvalues = [F(ray(pos, 0)), F(ray(pos, 5)), F(ray(pos, 10)), F(ray(pos, 15))];
        poly = B*polyvalues;
        //print(poly);
        froot = firstroot(poly);
       // print(froot);
        if(froot == oo,
          blue(.3),
          n = dF(ray(pos, froot));
          n = n/abs(n);
          red(n*[1,2,0])+green(n*[0,1,-2])+blue(n*[-2,0,1])
        )
      );

      
    
      
      use("CindyGL");
      t0 = seconds();
      createimage("plot",100,100);
    </script>
    

    <canvas  id="CSCanvas" width=500 height=500  style="border:2px solid #000000"></canvas>
    
    <script type="text/javascript">
        
        var gslp=[];
        createCindy({canvasname:"CSCanvas",
                    scripts: "cs*",
                    geometry:gslp,
                    autoplay: true,
                    transform:[{scale:1.},{translate:[0,0]}]
                  });
    </script>              
	</body>
</html>
