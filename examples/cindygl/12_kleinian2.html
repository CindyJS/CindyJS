<!DOCTYPE html>
<html lang="en">
	<head>
		<meta charset="utf-8">
    <title>Cindy JS</title>
    <script type="text/javascript" src="../../build/js/Cindy.js"></script>
    <script type="text/javascript" src="../../build/js/CindyGL.js"></script>
    <link rel="stylesheet" href="../../css/cindy.css">
  </head>
    
	<body style="font-family:Arial;">
    
    <h1>CindyJS: Generation of Limit Sets of Kleinian Groups Using a Feedback Loop Approach</h1>
    
    Press SPACE to reset everything. The red and the blue point determine the complex parameters ta, tb, which will be pluged into Grandma's recipe from Indra's Pearls: The Vision of Felix Klein, David Mumford, ‎Caroline Series, ‎David Wright.
  
               
    <script id="csinit" type="text/x-cindyscript">
      use("CindyGL");
      
      createimage("A", 1000, 1000); //last trafo
      createimage("a", 1000, 1000);
      createimage("B", 1000, 1000);
      createimage("b", 1000, 1000);
      createimage("C", 1000, 1000);
      createimage("c", 1000, 1000);
      colorplot([-2.5, -2.5], [2.5, -2.5], "A", 0.1); //initialize with some fog
      colorplot([-2.5, -2.5], [2.5, -2.5], "a", 0.1);
      colorplot([-2.5, -2.5], [2.5, -2.5], "B", 0.1);
      colorplot([-2.5, -2.5], [2.5, -2.5], "b", 0.1);
      colorplot([-2.5, -2.5], [2.5, -2.5], "C", 0.1);
      colorplot([-2.5, -2.5], [2.5, -2.5], "c", 0.1);
      
    </script>
    
    <script id="csmove" type="text/x-cindyscript">
      ta = .5*complex(TA)+.5+2;
      //ta = 1.64213876 +i*0.76658841;
      tb = .5*complex(TB)+.5+2;
      
      //Grandma's recipe from Indra's Pearls: The Vision of Felix Klein, David Mumford, ‎Caroline Series, ‎David Wright 
      tab = ta*tb/2-i*sqrt(-((ta*tb/2)^2-ta^2-tb^2));
      z0 = ((tab-2)*tb)/(tb*tab-2*ta+2*i*tab);
      a = [
        [ta/2,(ta*tab-2*tb+4*i)/((2*tab+4)*z0)],
        [(ta*tab-2*tb-4*i)*z0/(2*tab-4),ta/2]
      ];
      b = [
        [(tb-2*i)/2,tb/2],
        [tb/2,(tb+2*i)/2]
      ];
    
      
      A = inverse(a);
      B = inverse(b);
      
      c = a*b*A;
      C = inverse(c);
      

  </script>
      
  <script id="cskeydown" type="text/x-cindyscript">
      print("pressed key" + keycode());
      if(keycode()==32, 
        colorplot([-2.5, -2.5], [2.5, -2.5], "A", 0.1); //initialize with some fog
        colorplot([-2.5, -2.5], [2.5, -2.5], "a", 0.1);
        colorplot([-2.5, -2.5], [2.5, -2.5], "B", 0.1);
        colorplot([-2.5, -2.5], [2.5, -2.5], "b", 0.1);
        colorplot([-2.5, -2.5], [2.5, -2.5], "C", 0.1);
        colorplot([-2.5, -2.5], [2.5, -2.5], "c", 0.1);
      ); //space -> black
 </script>
    
    <script id="csdraw" type="text/x-cindyscript">
      dehomog(a) := a_1/a_2;
      moebius(A, z)  := (x = A*[z, 1]; x_1/x_2); //use homogenious coordinates
      dmoebius(A, z) := (x = A*[z, 1]; 1/re(x_2*conjugate(x_2))); //absolute value of deriviative
      
            
      T(a) := ( //maps [0,1] to [0, oo)
        res = a/(1-a);
        res*res);

      Tinv(a) := ( //maps [0,oo) to [0, 1]
        a = re(sqrt(a));
        a/(1+a);
      );

      readA(z) := imagergb([-2.5, -2.5], [2.5, -2.5], "A", (z)).r;
      reada(z) := imagergb([-2.5, -2.5], [2.5, -2.5], "a", (z)).r;
      readB(z) := imagergb([-2.5, -2.5], [2.5, -2.5], "B", (z)).r;
      readb(z) := imagergb([-2.5, -2.5], [2.5, -2.5], "b", (z)).r;
      readC(z) := imagergb([-2.5, -2.5], [2.5, -2.5], "C", (z)).r;
      readc(z) := imagergb([-2.5, -2.5], [2.5, -2.5], "c", (z)).r;
      
      compose(f, a, b, c, d, e) := (
        Tinv(f*f*(T(a)+T(b)+T(c)+T(d)+T(e)))+.0001
      );
      
      
      
      colorplot([-2.5, -2.5], [2.5, -2.5], "a",
        r = moebius(a, (complex(#)));
        compose(dmoebius(a, (complex(#))), reada(r), readb(r), readB(r), 0, 0)
      );
      
      colorplot([-2.5, -2.5], [2.5, -2.5], "A",
        r = moebius(A, (complex(#)));
        compose(dmoebius(A, (complex(#))), readA(r), 0, readc(r), readC(r), 0)
      );
      
      colorplot([-2.5, -2.5], [2.5, -2.5], "b",
        r = moebius(b, (complex(#)));
        compose(dmoebius(b, (complex(#))), reada(r), readA(r), readb(r), readc(r), readC(r))
      );
      
      colorplot([-2.5, -2.5], [2.5, -2.5], "B",
        r = moebius(B, (complex(#)));
        compose(dmoebius(B, (complex(#))), reada(r), readA(r), readB(r), readc(r), readC(r))
      );
      
      colorplot([-2.5, -2.5], [2.5, -2.5], "c",
        r = moebius(c, (complex(#)));
        compose(dmoebius(c, (complex(#))), reada(r), readA(r), readB(r), readb(r), readc(r))
      );
      
      colorplot([-2.5, -2.5], [2.5, -2.5], "C",
        r = moebius(C, (complex(#)));
        compose(dmoebius(C, (complex(#))), reada(r), readA(r), readB(r), readb(r), readC(r))
      );
      
      
      
      colorplot( //crops internal textures from 1000x1000 to 1000x1000
        z = (complex(#));
        s0 = readA(z);
        s1 = reada(z);
        s2 = readB(z);
        s3 = readb(z);
        s4 = readC(z);
        s5 = readc(z);
        sum = (s0+s1+s2+s3+s4+s5);
        gray(sum)+
        s0*hue(0/6)+s1*hue(1/6)+s2*hue(2/6)+s3*hue(3/6)+s4*hue(4/6)+s5*hue(5/6)
      );
      
      drawtext(TA+(.01,.01), ta, color->[1,0.3,0.3]);
      drawtext(TB+(.01,.01), tb, color->[0.3,0.3,1]);
      
       pt(a) := (re(a), im(a));
      pA = pt((moebius(A, (complex(Te))))); drawtext(pA, "A", color->hue(0/6));
      pa = pt((moebius(a, (complex(Te))))); drawtext(pa, "a", color->hue(1/6));
      pB = pt((moebius(B, (complex(Te))))); drawtext(pB, "B", color->hue(2/6));
      pb = pt((moebius(b, (complex(Te))))); drawtext(pb, "b", color->hue(3/6));
      pC = pt((moebius(C, (complex(Te))))); drawtext(pC, "C", color->hue(4/6));
      pc = pt((moebius(c, (complex(Te))))); drawtext(pc, "c", color->hue(5/6));
     
   
      
      
    </script>

    <canvas  id="CSCanvas" style="position:relative; top:10px;"></canvas>
    <script type="text/javascript">
        
        cdy = createCindy({canvasname:"CSCanvas",
                    scripts: "cs*",
                    geometry: [{name:"TA", type:"Free", pos:[-1 ,0.],color:[1,.3,.3],pinned:false,size:6},
                               {name:"TB", type:"Free", pos:[-1 ,0.],color:[.3,.3,1],pinned:false,size:6},
                               {name:"Te", type:"Free", pos:[0 ,0.],color:[1,.7,0],pinned:false,size:6}],
                    autoplay: true,
                    ports: [{
                      id: "CSCanvas",
                      width: 800,
                      height: 800,
                      transform: [ { visibleRect: [-2, -2, 2, 2] } ]
                    }]
                  });
    </script>              
	</body>
</html>
